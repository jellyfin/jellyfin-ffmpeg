# Maintainer: Peter Budai <peterbudai at hotmail.com>

_realname=opencl-icd
pkgbase=mingw-w64-jellyfin-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-jellyfin-${_realname}"
replaces=("${MINGW_PACKAGE_PREFIX}-jellyfin-${_realname}-git")
pkgver=2023.04.17
pkgrel=1
pkgdesc="OpenCL ICD Loader (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
msys2_references=(
  'aur: mingw-w64-opencl-icd'
)
url='https://github.com/KhronosGroup/OpenCL-ICD-Loader'
license=('spdx:Apache-2.0')
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-opencl-headers"
             "${MINGW_PACKAGE_PREFIX}-tools")
source=("https://github.com/KhronosGroup/OpenCL-ICD-Loader/archive/v${pkgver}/${_realname}-${pkgver}.tar.gz"
        "001-do-not-use-mingw32-def-on-clang32.patch"
        "OpenCL.pc")
sha256sums=('SKIP'
            'eb947c742dd31f2d1cc18ec8eb9af686385cc8634a9c56a3c4fb36d530d8280f'
            'SKIP')

export MINGW_TOOLCHAIN_PREFIX="${MINGW_PREFIX}"
export FF_MINGW_PREFIX="${MINGW_TOOLCHAIN_PREFIX}/ffbuild"

prepare() {
  cd "${srcdir}"/OpenCL-ICD-Loader-${pkgver}
  patch -p1 -i "${srcdir}"/001-do-not-use-mingw32-def-on-clang32.patch
}

build() {
  export CMAKE_PREFIX_PATH="${FF_MINGW_PREFIX}"
  export PKG_CONFIG_PATH="${FF_MINGW_PREFIX}/lib/pkgconfig:${PKG_CONFIG_PATH}"
  mkdir -p "${srcdir}"/build-${MSYSTEM} && cd "${srcdir}"/build-${MSYSTEM}

  declare -a extra_config
  if check_option "debug" "n"; then
    extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    ${MINGW_TOOLCHAIN_PREFIX}/bin/cmake \
      -G'Ninja' \
      -DCMAKE_INSTALL_PREFIX=${FF_MINGW_PREFIX} \
      "${extra_config[@]}" \
      -DBUILD_SHARED_LIBS=OFF \
      -DOPENCL_ICD_LOADER_DISABLE_OPENCLON12=ON \
      -DOPENCL_ICD_LOADER_HEADERS_DIR=${FF_MINGW_PREFIX}/include \
      -DBUILD_TESTING=OFF \
      ../OpenCL-ICD-Loader-${pkgver}

  ${MINGW_TOOLCHAIN_PREFIX}/bin/cmake --build .
}

package() {
  cd "${srcdir}"/build-${MSYSTEM}
  DESTDIR=${pkgdir} ${MINGW_TOOLCHAIN_PREFIX}/bin/cmake --install .

  # Replace the Pkg-config file
  rm -f "${pkgdir}${FF_MINGW_PREFIX}/lib/pkgconfig/OpenCL.pc"
  install -Dm644 "${srcdir}/OpenCL.pc"  "${pkgdir}${FF_MINGW_PREFIX}/lib/pkgconfig/OpenCL.pc"
  sed -e "s|@PREFIX@|${FF_MINGW_PREFIX}|g" \
      -i "${pkgdir}${FF_MINGW_PREFIX}/lib/pkgconfig/OpenCL.pc"

  install -Dm644 ${srcdir}/OpenCL-ICD-Loader-${pkgver}/LICENSE ${pkgdir}${FF_MINGW_PREFIX}/share/licenses/${_realname}/LICENSE
}
